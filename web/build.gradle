buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

apply plugin: 'war'
apply plugin: "com.moowork.node"

group = 'org.hillview'
version = '1.0-SNAPSHOT'

description = "web"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile project(':platform')
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    compile group: 'io.reactivex', name: 'rxjava', version: '1.2.9'
    compile group: 'org.apache.tomcat', name: 'tomcat-websocket', version: '8.5.8'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '2.0.1'
    compile group: 'com.univocity', name: 'univocity-parsers', version: '2.5.5'
    compile group: 'org.jblas', name: 'jblas', version: '1.2.4'
    compile group: 'it.unimi.dsi', name: 'dsiutils', version: '2.4.0'
    compile group: 'net.openhft', name: 'zero-allocation-hashing', version: '0.8'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

node {
    download = true
    nodeModulesDir = file("${project.projectDir}/webapp")
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    workingDir = node.nodeModulesDir
    script = file("${node.nodeModulesDir}/node_modules/webpack/bin/webpack.js")
    args = ['--display-modules']
    doLast {
        copy {
            from node.nodeModulesDir
            into "$buildDir/static-ui"
            include 'bundle.js'
            include 'bundle.js.map'
            include '*.html'
            include '*.png'
        }
        delete "${node.nodeModulesDir}/bundle.js"
        delete "${node.nodeModulesDir}/bundle.js.map"
    }
}

war.dependsOn(webpack)

war {
    from "$buildDir/static-ui"
    webXml = file('webapp/WEB-INF/web.xml')
}